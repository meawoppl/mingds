plugins {
    // Apply the java-library plugin to add support for Java Library
    id 'java-library'
    id 'antlr'
    id "com.diffplug.spotless" version "5.12.4"
}


group = 'io.txcl.mingds'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    api 'org.apache.commons:commons-math3:3.6.1'
    implementation 'com.google.guava:guava:27.0.1-jre'
    implementation group: 'org.reflections', name: 'reflections', version: '0.9.12'

    // Use JUnit test framework
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.5.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.5.2"
    testRuntimeOnly "org.junit.platform:junit-platform-runner:1.5.2"

    // https://mvnrepository.com/artifact/org.antlr/antlr4-runtime
    antlr 'org.antlr:antlr4:4.7.1'
    implementation "org.antlr:antlr4-intellij-adaptor:0.1"

    // argparse
    api 'net.sourceforge.argparse4j:argparse4j:0.2.0'
}

// Make tests run via the CLI make some noise
test {
    useJUnitPlatform()

    afterTest { desc, result ->
        logger.quiet "Executing test [${desc.className}]->${desc.name} with result: ${result.resultType}"
    }
}

//NOTE(G3): Gradle v7.0 was throwing some noisy implicit task dependency warnings
// that were disabling execution optimizations.. This explicitly sets those dependenices
project.afterEvaluate {
    spotlessJava.dependsOn(
            'compileJava',
            'compileTestJava',
            'processResources',
            'processTestResources',
            'test'
    )
}

spotless {
    java {
        googleJavaFormat('1.7').aosp()
        target project.fileTree(project.rootDir) {
            include '**/*.java'
            exclude 'build/**/*.*'
        }
    }
}
